// --- VOID THEME COLORS ---
const vec3 C_BG = vec3(0.0, 0.0, 0.0);           // #000000
const vec3 C_TEAL = vec3(0.329, 0.627, 0.573);   // #54a092 (Ghostty Cursor Color)
const vec3 C_CYAN = vec3(0.0, 1.0, 0.976);       // #00fff9 (Bright Cyan Accent)

// --- BLEND CALCULATION ---
const float BLEND_FACTOR = 0.75;
#define BLEND_BG(color, factor) mix(C_BG, color, factor)

// --- CONFIGURATION ---
vec4 TRAIL_COLOR = vec4(C_TEAL, 0.8);
vec4 TAIL_COLOR_CONFIG = vec4(C_CYAN, 0.3);

const float DURATION = 0.22; 
const float TRAIL_SIZE = 0.8; 
const float THRESHOLD_MIN_DISTANCE = 1.0; 
const float BLUR = 3.0; // Softer, smoother edges for better background integration
const float TRAIL_THICKNESS = 0.95;
const float TRAIL_THICKNESS_X = 0.95;

const float FADE_ENABLED = 1.0; 
const float FADE_EXPONENT = 3.5; // Smoother, more natural exponential fade

// --- CONSTANTS ---
const float PI = 3.14159265359;

float ease(float x) {
    return 1.0 - pow(1.0 - x, 4.0); // EaseOutQuart
}

float getSdfRectangle(in vec2 p, in vec2 xy, in vec2 b)
{
    vec2 d = abs(p - xy) - b;
    return length(max(d, 0.0)) + min(max(d.x, d.y), 0.0);
}

float seg(in vec2 p, in vec2 a, in vec2 b, inout float s, float d) {
    vec2 e = b - a;
    vec2 w = p - a;
    vec2 proj = a + e * clamp(dot(w, e) / dot(e, e), 0.0, 1.0);
    float segd = dot(p - proj, p - proj);
    d = min(d, segd);
    float c0 = step(0.0, p.y - a.y);
    float c1 = 1.0 - step(0.0, p.y - b.y);
    float c2 = 1.0 - step(0.0, e.x * w.y - e.y * w.x);
    float flip = mix(1.0, -1.0, step(0.5, (c0 * c1 * c2) + ((1.0 - c0) * (1.0 - c1) * (1.0 - c2))));
    s *= flip;
    return d;
}

float getSdfConvexQuad(in vec2 p, in vec2 v1, in vec2 v2, in vec2 v3, in vec2 v4) {
    float s = 1.0;
    float d = dot(p - v1, p - v1);
    d = seg(p, v1, v2, s, d);
    d = seg(p, v2, v3, s, d);
    d = seg(p, v3, v4, s, d);
    d = seg(p, v4, v1, s, d);
    return s * sqrt(d);
}

vec2 normalize(vec2 value, float isPosition) {
    return (value * 2.0 - (iResolution.xy * isPosition)) / iResolution.y;
}

float antialising(float distance, float blurAmount) {
  return 1. - smoothstep(0., normalize(vec2(blurAmount, blurAmount), 0.).x, distance);
}

float getDurationFromDot(float dot_val, float DURATION_LEAD, float DURATION_SIDE, float DURATION_TRAIL) {
    float isLead = step(0.5, dot_val);
    float isSide = step(-0.5, dot_val) * (1.0 - isLead);
    return mix(mix(DURATION_TRAIL, DURATION_SIDE, isSide), DURATION_LEAD, isLead);
}

void mainImage(out vec4 fragColor, in vec2 fragCoord){
    #if !defined(WEB)
    fragColor = texture(iChannel0, fragCoord.xy / iResolution.xy);
    #endif

    vec2 vu = normalize(fragCoord, 1.);
    vec2 offsetFactor = vec2(-.5, 0.5);

    vec4 currentCursor = vec4(normalize(iCurrentCursor.xy, 1.), normalize(iCurrentCursor.zw, 0.));
    vec4 previousCursor = vec4(normalize(iPreviousCursor.xy, 1.), normalize(iPreviousCursor.zw, 0.));

    vec2 centerCC = currentCursor.xy - (currentCursor.zw * offsetFactor);
    vec2 halfSizeCC = currentCursor.zw * 0.5;
    vec2 centerCP = previousCursor.xy - (previousCursor.zw * offsetFactor);

    float sdfCurrentCursor = getSdfRectangle(vu, centerCC, halfSizeCC);
    float lineLength = distance(centerCC, centerCP);
    float minDist = currentCursor.w * THRESHOLD_MIN_DISTANCE;

    vec4 newColor = vec4(fragColor);
    float baseProgress = iTime - iTimeCursorChange;

    if (lineLength > minDist && baseProgress < DURATION - 0.001) {
        float cc_half_height = currentCursor.w * 0.5;
        float cc_center_y = currentCursor.y - cc_half_height;
        float cc_new_top_y = cc_center_y + cc_half_height * TRAIL_THICKNESS;
        float cc_new_bottom_y = cc_center_y - cc_half_height * TRAIL_THICKNESS;

        float cc_half_width = currentCursor.z * 0.5;
        float cc_center_x = currentCursor.x + cc_half_width;
        float cc_new_left_x = cc_center_x - cc_half_width * TRAIL_THICKNESS_X;
        float cc_new_right_x = cc_center_x + cc_half_width * TRAIL_THICKNESS_X;

        vec2 cc_tl = vec2(cc_new_left_x, cc_new_top_y);
        vec2 cc_tr = vec2(cc_new_right_x, cc_new_top_y);
        vec2 cc_bl = vec2(cc_new_left_x, cc_new_bottom_y);
        vec2 cc_br = vec2(cc_new_right_x, cc_new_bottom_y);

        float cp_half_height = previousCursor.w * 0.5;
        float cp_center_y = previousCursor.y - cp_half_height;
        float cp_new_top_y = cp_center_y + cp_half_height * TRAIL_THICKNESS;
        float cp_new_bottom_y = cp_center_y - cp_half_height * TRAIL_THICKNESS;

        float cp_half_width = previousCursor.z * 0.5;
        float cp_center_x = previousCursor.x + cp_half_width;
        float cp_new_left_x = cp_center_x - cp_half_width * TRAIL_THICKNESS_X;
        float cp_new_right_x = cp_center_x + cp_half_width * TRAIL_THICKNESS_X;

        vec2 cp_tl = vec2(cp_new_left_x, cp_new_top_y);
        vec2 cp_tr = vec2(cp_new_right_x, cp_new_top_y);
        vec2 cp_bl = vec2(cp_new_left_x, cp_new_bottom_y);
        vec2 cp_br = vec2(cp_new_right_x, cp_new_bottom_y);

        const float DURATION_TRAIL = DURATION;
        const float DURATION_LEAD = DURATION * (1.0 - TRAIL_SIZE);
        const float DURATION_SIDE = (DURATION_LEAD + DURATION_TRAIL) / 2.0;

        vec2 moveVec = centerCC - centerCP;
        vec2 s = sign(moveVec);

        float dur_tl = getDurationFromDot(dot(vec2(-1., 1.), s), DURATION_LEAD, DURATION_SIDE, DURATION_TRAIL);
        float dur_tr = getDurationFromDot(dot(vec2( 1., 1.), s), DURATION_LEAD, DURATION_SIDE, DURATION_TRAIL);
        float dur_bl = getDurationFromDot(dot(vec2(-1.,-1.), s), DURATION_LEAD, DURATION_SIDE, DURATION_TRAIL);
        float dur_br = getDurationFromDot(dot(vec2( 1.,-1.), s), DURATION_LEAD, DURATION_SIDE, DURATION_TRAIL);

        float isMovingRight = step(0.5, s.x);
        float isMovingLeft  = step(0.5, -s.x);

        float dur_right_rail = getDurationFromDot((dot(vec2(1.,1.),s)+dot(vec2(1.,-1.),s))*0.5, DURATION_LEAD, DURATION_SIDE, DURATION_TRAIL);
        float dur_left_rail = getDurationFromDot((dot(vec2(-1.,1.),s)+dot(vec2(-1.,-1.),s))*0.5, DURATION_LEAD, DURATION_SIDE, DURATION_TRAIL);

        float final_dur_tl = mix(dur_tl, dur_left_rail, isMovingLeft);
        float final_dur_bl = mix(dur_bl, dur_left_rail, isMovingLeft);
        float final_dur_tr = mix(dur_tr, dur_right_rail, isMovingRight);
        float final_dur_br = mix(dur_br, dur_right_rail, isMovingRight);

        vec2 v_tl = mix(cp_tl, cc_tl, ease(clamp(baseProgress / final_dur_tl, 0.0, 1.0)));
        vec2 v_tr = mix(cp_tr, cc_tr, ease(clamp(baseProgress / final_dur_tr, 0.0, 1.0)));
        vec2 v_br = mix(cp_br, cc_br, ease(clamp(baseProgress / final_dur_br, 0.0, 1.0)));
        vec2 v_bl = mix(cp_bl, cc_bl, ease(clamp(baseProgress / final_dur_bl, 0.0, 1.0)));

        float sdfTrail = getSdfConvexQuad(vu, v_tl, v_tr, v_br, v_bl);
        float fadeProgress = clamp(dot(vu - centerCP, moveVec) / (dot(moveVec, moveVec) + 1e-6), 0.0, 1.0);

        vec4 trail = mix(TAIL_COLOR_CONFIG, TRAIL_COLOR, fadeProgress);
        float shapeAlpha = antialising(sdfTrail, mix(0.0, BLUR, abs(s.x) * abs(s.y))); 

        if (FADE_ENABLED > 0.5) {
            trail.a *= pow(fadeProgress, FADE_EXPONENT);
        }

        newColor = mix(newColor, vec4(trail.rgb, newColor.a), trail.a * shapeAlpha);
        newColor = mix(newColor, fragColor, step(sdfCurrentCursor, 0.));
    }
    fragColor = newColor;
}
